![Capsule Render](https://capsule-render.vercel.app/api?type=waving&height=130&color=FF0084&text=üí°%20Jogo%20de%20Perguntas%20com%20Arduino%20üí°&section=header&reversal=false&fontSize=30&fontColor=FFCBDB&fontAlignY=65)

<p align="center">
  <a href="#-objetivo-">Objetivo</a> |
  <a href="#-itens-utilizados-">Itens Utilizados</a> |
  <a href="#%EF%B8%8F-passo-a-passo-do-jogo-%EF%B8%8F">Passo a Passo</a> |
  <a href="#-v√≠deos-">V√≠deos</a> |
  <a href="#-autores-do-projeto-">Autores</a>
</p>

## üìà Objetivo üìà

O objetivo do jogo de perguntas √© oferecer uma experi√™ncia divertida e educativa aos jogadores, enquanto exploram seus conhecimentos em diversos temas. Com tr√™s n√≠veis de dificuldade - f√°cil, m√©dio e dif√≠cil - cada um com cinco perguntas e uma pergunta final, os jogadores t√™m a oportunidade de testar suas habilidades e aprender novos conceitos.

## üìö Itens Utilizados üìö

### Tecnologias üëæ

<div style="display: inline_block"><br>
  <img align="center" alt="Tinkercad" src="https://img.shields.io/badge/Tinkercad-1477D1.svg?style=for-the-badge&logo=Tinkercad&logoColor=white">
  <img align="center" alt="C++" src="https://img.shields.io/badge/C++-00599C.svg?style=for-the-badge&logo=c%2B%2B&logoColor=white">  
</div>

### Materiais üñåÔ∏è

<div>
  <ul>
    <li>01 Arduino UNO</li>
    <li>02 LEDs</li>
    <li>04 bot√µes</li>
    <li>07 Resistores</li>
    <li>01 Buzzer</li>
    <li>01 Display LCD 16x2</li>
    <li>02 Protoboards</li>
    <li>01 Potenci√¥metro</li>
  </ul>
</div>

### C√≥digo üëæ

Optamos por incluir o c√≥digo utilizado no projeto neste bloco para uma apresenta√ß√£o visual mais atraente, mas ele tamb√©m est√° dispon√≠vel no reposit√≥rio.
<details>
  <summary>C√≥digo em C++</summary>

```cpp
#include <LiquidCrystal.h>

LiquidCrystal lcd(13, 12, 7, 6, 5, 8);

//Come√ßamos definindo os pinos dos bot√µes, leds e buzzer, 
//assim facilita a identifica√ß√£o no decorrer do c√≥digo!
int botao_iniciar = 4;
int botao_sim = 2;
int botao_nao = A4;
int botao_pular = 3;
int led_verde = 10;
int led_vermelho = 11;
int buzzer = 9;

//Criamos arrays de perguntas e respostas para cada dificuldade, 
//gerando assim um banco de perguntas e suas respectivas perguntas!
String perguntas_faceis[] = {"1 + 1 = 2 ?" , "2 + 2 = 22 ?", "3 + 3 = 4 ?", "5 junto com 5 = 55 ?",
                             "6 - 9 = 3 ?", "3 - 3 = -3 ?", "1.5 + 1 = 2.5 ?", "0 + 0 = 0 ?"};
String perguntas_medias[] = {"1 + 1 + 5 - 1 =  -6 ?", "3 - 3 + 3 = 3 ?", " 0 - 1 + 9 = 8 ?",
                             "1 + 1 + 1 + 1 + 1 + 1 + 1 + 1 = 9 ?", "8 - 8 - 7 = -7 ?",
                             " 9 - 9 - 9 + 9 = 0 ?", " 0 + 0 = - 2 - 2 ?", "3 + 3 - 2 + 6 = 28?"};
String perguntas_dificeis[] = {"11 x 11 = 121 ?", "5 x 5 / 5 = 5 ?", "5 * 5 - 8 + 9 / 1 = 1 ?",
                               "2 * 2 + 2 = 8 ?", " 1 * 1 / 1 * 1 = 1 ?", " 5 * 6 - 7 + 8 = 31 ? ",
                               " 0 * 10 - 10 + 10 = -10 ?", " 60 + 60 / 60 = 2 ?"};
String pergunta_boss = "Tiramos 10?";

bool respostas_faceis[] = {true, false, false, true, false, false, true, true};
bool respostas_medias[] = {false, true, true, false, true, true, false, false};
bool respostas_dificeis[] = {true, true, false, false, true, true, false, false};
bool resposta_boss = true;

//Aqui criamos as vari√°veis de controle do jogo, 
//que definem algumas coisas b√°sicas para n√≥s, 
//como a quatidade de pulos, tentativas e algumas outras coisas.
int dificuldade = 0;
int acerto = 0;
int tentativa = 1;
int pulo = 4;
int pergunta_atual = 0;
unsigned long contagem_tempo_resposta;
const long tempo_resposta = 15000;
boolean jogo_iniciado = false;
int pontos = 0;
int numero_da_questao = 0;
String Isaac = "Selo Isaac de Aprovacao";

//Nesse ponto come√ßamos a criar nossas primeiras configura√ß√µes, 
//definindo pinos digitais e anal√≥gicos, para aos pinos que identificamos anteriormente, 
//podendo ser esses pinos de entrada ou sa√≠da
void setup() {
  lcd.begin(16, 2);
  pinMode(botao_iniciar, INPUT);
  pinMode(botao_sim, INPUT);
  pinMode(botao_nao, INPUT);
  pinMode(botao_pular, INPUT);
  pinMode(led_verde, OUTPUT);
  pinMode(led_vermelho, OUTPUT);
  pinMode(buzzer, OUTPUT);
  randomSeed(analogRead(0));
  lcd.print("Bora jogar!");
}

//Aqui temos o loop principal do jogo, √© nele que verificamos se o tempo para responder acabou,
//Iniciar o jogo e desistir do mesmo, verificar se o jogo est√° iniciado ou n√£o, 
//se ainda h√° pulos dispon√≠veis e se houverem, √© aqui que o pular pergunta acontece.
//Fora isso, √© no loop que ainda temos a exibi√ß√£o dos pontos no LCD.
void loop() {
  if (digitalRead(botao_iniciar) == HIGH) {
    delay(200);
    if (!jogo_iniciado) {
      inicia_jogo();
    } else {
      desistir_jogo();
    }
  } else if (jogo_iniciado) {
    jogo_loop();
    verifica_tempo();
  }

  if (digitalRead(botao_pular) == HIGH && jogo_iniciado) {
    delay(200);
    pular_pergunta();
  }

  if(jogo_iniciado) {
    lcd.setCursor(0, 1);
    lcd.print("Pontos: ");
    lcd.print(pontos);
  }
}

//Aqui temos a fun√ß√£o de in√≠cio do jogo.
//Nessa fun√ß√£o, temos leds indicadores do in√≠cio do jogo, 
//o rein√≠cio das vari√°veis e a sele√ßao da primeira pergunta.
void inicia_jogo() {
  jogo_iniciado = true;
  //leds
  digitalWrite(led_vermelho, HIGH);
  delay(200);
  digitalWrite(led_vermelho, LOW);
  digitalWrite(led_verde, HIGH);
  delay(200);
  digitalWrite(led_verde, LOW);
  //vari√°veis reiniciadas e sele√ß√£o da primeira pergunta
  dificuldade = 0;
  acerto = 0;
  tentativa = 1;
  pontos = 0;
  pulo = 4;
  pergunta_atual = 0;
  numero_da_questao = 1;
  seleciona_pergunta();
  apresenta_pergunta();
  contagem_tempo_resposta = millis();
}

//Essa fun√ß√£o, por sua vez, faz com que haja desist√™ncia,
//Ent√£o assim que clicamos novamente no bot√£o de iniciar,
//O Jogo automaticamente √© "cancelado" e uma mensagem aparece no lcd.
//Ap√≥s isso, o jogo reinicia.
void desistir_jogo() {
  jogo_iniciado = false;
  som_desistencia();
  lcd.clear();
  lcd.print("Fraco!");
  delay(2000);
  lcd.clear();
  lcd.print("Bora jogar!");
}

//Essa √© a fun√ß√£o chamada durante o jogo para a escolha da pergunta.
//Junto daquele "banco" de perguntas, essa fun√ß√£o escolhe uma pergunta,
//Para ser apresentada em outra fun√ß√£o.
//Aqui tamb√©m h√° a troca das perguntas de acordo com a dificuldade.
void seleciona_pergunta() {
  switch (dificuldade) {
    case 0: pergunta_atual = random(0, sizeof(perguntas_faceis) / sizeof(perguntas_faceis[0])); break;
    case 1: pergunta_atual = random(0, sizeof(perguntas_medias) / sizeof(perguntas_medias[0])); break;
    case 2: pergunta_atual = random(0, sizeof(perguntas_dificeis) / sizeof(perguntas_dificeis[0])); break;
  }
}

//Essa √© a fun√ß√£o que apresenta a pergunta escolhida pela fun√ß√£o anterior.
//Fora isso, tamb√©m temos a impress√£o no led das duas op√ß√µes de resposta (Sim e N√£o),
//A quantidade de pontos e em qual pergunta das 16, o player est√°.
void apresenta_pergunta() {
  jogo_iniciado = true;
  lcd.clear();
  mostra_pergunta_rolando();
  lcd.clear();
  lcd.print("Sim ou Nao?");
  lcd.setCursor(0, 1);
  lcd.print("Score:");
  lcd.print(pontos);
  numero_da_questao++;
  lcd.print("   Q:");
  lcd.print(numero_da_questao -1);
  contagem_tempo_resposta = millis();
}

//Essa fun√ß√£o serve para que perguntas grandes, 
//Que ultrapassam o limite de 16 caracteres apresent√°veis no lcd,
//Possam ser apresentadas roladno da direita para esquerda.
void mostra_pergunta_rolando() {
  String pergunta_completa = "";
  switch (dificuldade) {
    case 0: pergunta_completa = perguntas_faceis[pergunta_atual]; break;
    case 1: pergunta_completa = perguntas_medias[pergunta_atual]; break;
    case 2: pergunta_completa = perguntas_dificeis[pergunta_atual]; break;
    case 3: pergunta_completa = pergunta_boss; break;
  }

  if (pergunta_completa.length() <= 16) {
    lcd.print(pergunta_completa);
    delay(4000);
  } else {
    for (int i = 0; i < pergunta_completa.length() - 16; i++) {
      lcd.clear();
      lcd.print(pergunta_completa.substring(i, i + 16));
      delay(500);
    }
    delay(1000);
  }
}

//Aqui temos a valida√ß√£o do pressionamento do bot√£o,
//Ou seja, ele interpreta o bot√£o apertado e manda para outra fun√ß√£o,
//O que o player respondeu, e nessa outra fun√ß√£o, o jogo verifica se est√° correto.
void jogo_loop() {
  if (digitalRead(botao_sim) == HIGH) {
    delay(200);
    verifica_resposta(true);
  } else if (digitalRead(botao_nao) == HIGH) {
    delay(200);
    verifica_resposta(false);
  }
}

//Essa √© a fun√ß√£o que recebe a interpreta√ß√£o dos bot√µes e verifica se est√° correta ou n√£o, a resposta.
//Fora isso temos diferentes sons emitidos nas respostas corretas e incorretas.
//Temos leds indicadores de erro e acerto. Som emitido quando se perde o jogo, e a verifica√ßao das respostas,
//De acordo com a dificuldade. Fora o som emitido quando vencemos!
//Lembrando que todos esses sons s√£o configurados em outras fun√ß√µes!
void verifica_resposta(bool resposta) {
  bool resposta_correta = false;
  if (dificuldade < 3) {
    switch (dificuldade) {
      case 0: resposta_correta = respostas_faceis[pergunta_atual]; break;
      case 1: resposta_correta = respostas_medias[pergunta_atual]; break;
      case 2: resposta_correta = respostas_dificeis[pergunta_atual]; break;
    }

    if (resposta == resposta_correta) {
      acerto++;
      som_acerto();
      pontos++;
      if (acerto >= 5) {
        dificuldade++;
        acerto = 0;
        if (dificuldade == 3) {
          lcd.clear();
          lcd.print(pergunta_boss);
        } else {
          seleciona_pergunta();
          apresenta_pergunta();
        }
      } else {
        seleciona_pergunta();
        apresenta_pergunta();
      }
    } else {
      som_erro();
      tentativa--;
      digitalWrite(led_vermelho, HIGH);
      delay(200);
      digitalWrite(led_vermelho, LOW);
      if (tentativa == 0) {
        jogo_iniciado = false;
        som_desistencia();
        lcd.clear();
        lcd.print("Ja era meu fi");
        delay(3000);
        return;
      }
      seleciona_pergunta();
      apresenta_pergunta();
    }
  } else if (dificuldade == 3) {
    if (resposta == resposta_boss) {
      som_vencer();
      jogo_iniciado = false;
      lcd.clear();
      lcd.print(Isaac);
      delay(1500);
      for (int i = 0; i < Isaac.length()-16; i++){
        lcd.scrollDisplayLeft();
        delay(500);
      }
    } else {
      som_erro();
      lcd.clear();
      lcd.print("Poxa Isaac...");
      delay(3000);
      jogo_iniciado = false;
    }
  }
  contagem_tempo_resposta = millis();
}

//Aqui temos a fun√ß√£o que verifica o tempo para responder, e envia essa informa√ß√£o para o loop principal.
//Quando o tempo acaba, o jogo entende que a pergunta n√£o foi respondida e portanto a resposta est√° incorreta.
//Com isso ele desconta a tentativa, essa que era √∫nica e portanto o jogo √© perdido. E ap√≥s isso, reiniciado.
void verifica_tempo() {
  unsigned long tempo_atual = millis();
  if (tempo_atual - contagem_tempo_resposta > tempo_resposta) {
    som_erro();
    tentativa--;
    if (tentativa == 0) {
      som_desistencia();
      jogo_iniciado = false;
      lcd.clear();
      lcd.print("Ja era meu fi");
      delay(3000);
      return;
    }
    seleciona_pergunta();
    apresenta_pergunta();
    contagem_tempo_resposta = millis();
  } else if (tempo_atual - contagem_tempo_resposta > tempo_resposta - 5000) {
    digitalWrite(led_vermelho, (millis() / 250) % 2);
  } else {
    digitalWrite(led_vermelho, LOW);
  }
}

//Fun√ß√£o de configura√ß√£o do buzzer para acerto
void som_acerto() {
  tone(buzzer, 1000, 200);
  digitalWrite(led_verde, HIGH);
  delay(200);
  digitalWrite(led_verde, LOW);
  noTone(buzzer);
}

//Fun√ß√£o de configura√ß√£o do buzzer para erro
void som_erro() { 
  tone(buzzer, 250, 200); 
  delay(300); 
  noTone(buzzer); 
}

//Fun√ß√£o de configura√ß√£o do buzzer para desist√™ncia
void som_desistencia() { 
  for (int i = 0; i < 3; i++) { 
  tone(buzzer, 200, 300); 
  delay(350); 
  } 
  noTone(buzzer); 
}

//Fun√ß√£o de configura√ß√£o do buzzer para quando vencer o jogo e led piscante indicando sucesso!
void som_vencer() {
  tone(buzzer, 523, 300);
  delay(300);
  tone(buzzer, 587, 300);
  delay(300);
  tone(buzzer, 659, 300);
  delay(300);
  tone(buzzer, 698, 300);
  delay(300);
  tone(buzzer, 784, 300);
  delay(300);
  noTone(buzzer);

  for(int i = 0; i < 10; i++) {
    digitalWrite(led_verde, HIGH);
    delay(100);
    digitalWrite(led_verde, LOW);
    delay(100);
  }
}

//Essa foi a √∫ltima fun√ß√£o arrumada e por isso ficou separada de todas as outras.
//Ela √© a fun√ß√£o que pula as perguntas, sendo no m√°ximo 3 pulos, e se o player tentare uma quarta vez,
//Ele perde o jogo.
void pular_pergunta() {
  pulo--;
    if (pulo == 0) {
      som_desistencia();
      jogo_iniciado = false;
      lcd.clear();
      lcd.print("Ja era meu fi");
      delay(3000);
      return;
    } else {
      seleciona_pergunta();
      apresenta_pergunta();
    }
}
```
</details>

### Circuito Montado üñåÔ∏è
<div>
  <img src="IMAGENS/PJ_TINKER.jpeg" width="1000">
</div> 
<br>

Clique no bot√£o e tenha acesso ao circuito ‚¨áÔ∏è 

<a href="https://www.tinkercad.com/things/7hIbVrCa4DK-projeto-final/editel?sharecode=hfgsll-JCRzcpZOkn3FswTL0HCJUgorU_o3cYWIUQLw">
  <img src="https://img.shields.io/badge/CLICK%20HERE!!-FF0084" alt="Clique Aqui!">
</a>

## üïπÔ∏è Passo a Passo do Jogo üïπÔ∏è

- Assim que o LCD ligar, o primeiro bot√£o, da esquerda para direita dever√° ser pressionado para que o `jogo inicie`.
- O jogo est√° dividido em 03 n√≠veis diferentes, o f√°cil, o m√©dio e o dificil, sendo cada um composto por 05 perguntas cada.
- Assim que as perguntas iniciarem, o jogador ter√° `15 segundos` antes que a pergunta pule.
- Caso o mesmo queira `pular a pergunta`, o segundo bot√£o da esquerda para a direita dever√° ser pressionado.
- Para respoder `SIM`, o jogador dever√° pressionar o segundo bot√£o da direita para a esquerda, caso queira responder `N√ÉO`, o mesmo dever√° pressionar o
primeiro bot√£o da direita para a esquerda.
- Caso queira desistir do jogo, o mesmo bot√£o que foi usado para iniciar o jogo dever√° ser pressionado novamente para o jogo finalizar.

  ### Acontecimentos do Jogo üéÆ
  - Quando o jogador `acertar` uma pergunta, o `led verde` acender√° e um som ser√° emitido pelo Buzzer.
  - Caso o mesmo `erre` a pergunta, o `led vermelho` acender√°, o Buzzer emitir√° um som e um aviso aparecer√° no LCD informando que o jogo encerrou.
  - Assim que as 15 perguntas forem respondidas corretamente, uma `√∫ltima pergunta` aparecer√° no LCD e caso o jogador acerte, o `jogo finalizar√°` com uma m√∫sica no Buzzer e uma mensagem no LCD.
  - Caso o mesmo `erre`, o jogo finalizar√° com outro som.

## üì∏ V√≠deos üì∏

No dia da apresenta√ß√£o, fizemos algumas grava√ß√µes do jogo rodando. Caso queira ver como ficou, basta clicar no bot√£o abaixo.

<a href="https://www.youtube.com/playlist?list=PLo3Nzu21r7jqFXAwNyTGfYLX6Vez5UGqC">
  <img src="https://img.shields.io/badge/YouTube-FF0000.svg?style=for-the-badge&logo=YouTube&logoColor=white" alt="YouTube">
</a>

## üßëüèª‚Äçüíª Autores do Projeto üßëüèª‚Äçüíª

#### Anna Carolina Zomer ‚¨áÔ∏è
[![E-mail](https://img.shields.io/badge/GitHub-181717.svg?style=for-the-badge&logo=GitHub&logoColor=white)](https://github.com/z0mer)

#### Humberto Pellegrini ‚¨áÔ∏è
[![E-mail](https://img.shields.io/badge/GitHub-181717.svg?style=for-the-badge&logo=GitHub&logoColor=white)](https://github.com/Humbertin07)

![Capsule Render](https://capsule-render.vercel.app/api?type=waving&height=130&color=FF0084&text=üëãüèª%20At√©%20a%20Pr√≥xima!!%20üëãüèª&section=footer&reversal=false&fontSize=30&fontColor=FFCBDB&fontAlignY=40)
